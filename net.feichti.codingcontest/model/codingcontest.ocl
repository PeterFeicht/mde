import 'codingcontest.ecore'

package codingcontest

context Contest
	
	inv HasTheme: theme.size() > 0
	
	-- There should be unique, consecutive levels starting at 0 or 1
	inv NoDuplicateLevels: levels->isUnique(it: Level | it.number)
	inv ConsecutiveLevels:
		let numbers = levels->collect(it | it.number) in
		(numbers->max() - numbers->min() + 1) = levels->size()
	inv MinLevel('The first level should have number 0 or 1'):
		let first = levels->sortedBy(it | it.number)->first()?.number in
		first = 0 or first = 1
	
	-- Each problem must be available in every team's language
	inv NoMissingLang:
		let needed = entries->collect(it | it.language)->asSet() in
		levels->forAll(level | (needed - level.problems->collect(it | it.key)->asSet())->isEmpty())

context Location
	
	inv HasName: name.size() > 0
	inv HasAddress: address.size() > 0
	inv HasCountry: country.size() > 0

context Team
	
	inv HasTeamName: name.size() > 0

context Coder
	
	inv HasName('Coder "' + self.toString() + '" must have a name'):
		name.size() > 0
		
	inv PositiveAge('Age must be positive'):
		age > 0
	inv StillAlive('I don\'t believe you are ' + age.toString() + ' years old'):
		age < 150

context Organizer
	
	inv HasName('Organizer "' + self.toString() + '" must have a name'):
		name.size() > 0

context Problem
	
	inv HasDescription: description.size() > 0

endpackage
